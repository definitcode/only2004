[opnpc1,notnurmof]

~chatnpc("<p,happy>Welcome to the Not Nurmof Store.| What can I do for you?");
switch_int(~p_choice4("I'd like to check your shop.", 1, "Nothing!", 2, "Run Essence For Me (150gp)!", 3, "Can you combine my Rune Fragments?", 4)) {
    case 1 : {
        ~chatplayer("<p,happy>I'd like to check your shop.");
        ~openshop_activenpc;
    }
    case 2 : {
        ~chatplayer("<p,neutral>Nothing!");
    }
    case 3 : {
        ~chatplayer("<p,happy>Run Essence For Me (150gp)!");
        // @notnurmofcheck;
        def_int $total_ess = inv_total(inv, blankrune);

        if (inv_total(inv, coins) > 149) {
            inv_del(inv, coins, 150);
        } else {
            ~chatnpc("<p,sad>You do not have enough coins!");
            return;
        }
        
        if (inv_freespace(bank) > 0) {
            inv_del(inv, blankrune, $total_ess);
            inv_add(bank, blankrune, $total_ess);
            ~chatnpc("I have moved [<tostring($total_ess)>x] to your bank!");
        } else {
            ~chatnpc("<p,sad>I am afraid you don't have bank space! | Here is your Coins back!");
            inv_add(inv, coins, 150);
            ~mesbox("You receive 150gp!");
        }
    }
    case 4 : {
        def_int $rune_fragmentcount = inv_total(inv, rune_fragment);
        def_int $rune_count = divide($rune_fragmentcount, 10); // Whole runes to give
        def_int $used_fragments = multiply($rune_count, 10);   // Actual fragments used
    
        if ($rune_count > 0) {
            inv_del(inv, rune_fragment, $used_fragments);
            inv_add(inv, cert_blankrune, $rune_count);
            ~chatnpc("Here is a certificate for [<tostring($rune_count)>x] Blank Runes!");
        } else {
            ~chatnpc("You need at least 10 fragments to get a Blank Rune.");
        }
    }
}