[oplocu,_smithing_furnace]
switch_obj (last_useitem)
{
    case gold_bar, perfect_gold_bar : @craft_gold_interface;
    case silver_bar : @craft_silver;
    case steel_bar : @smelt_cannonballs;
    case bucket_sand, soda_ash : @smelt_glass;
    case caveorb1, caveorb2, caveorb3, caveorb4 :
        if(loc_type = furnace_upass) @destroy_orboflight(last_useitem);
        ~displaymessage(^dm_default);
    case default :
        if (oc_category(last_useitem) = category_91) {
            @smelt_ore(last_useitem);
        }
        ~displaymessage(^dm_default);
};

//--- this is the code that uses p_delays instead of weakqueues
[label,smelt_ore](obj $last)
if (getqueue(smelting_repeat) > 0) {
    clearqueue(smelting_repeat);
}
p_arrivedelay();
// find the bar that the ore smelts to
def_namedobj $bar = oc_param($last, smeltsto);
// using coal on furnace assumes steel bar. If using iron on furnace and player has 2 or more coal, then assume steel bar
if (($last = iron_ore & inv_total(inv, coal) >= 2 & stat(smithing) >= 30) | $last = coal) {
    $bar = steel_bar;
}
// for some reason jagex has blankrune and clay in the ores category
if ($bar = null) {
    ~displaymessage(^dm_default);
    return;
}
def_struct $struct = oc_param($bar, smelting_struct); 
// If smithing level isnt high enough, dislpay level fail message
if (stat(smithing) < struct_param($struct, levelrequired)) {
    ~mesbox(struct_param($struct, levelfailure));
    return;
}
def_int $primary_total = inv_total(inv, struct_param($struct, ingredient));
def_int $secondary_total = inv_total(inv, struct_param($struct, ingredient_secondary));
// If not enough ores
if ($primary_total < struct_param($struct, bar_count)) {
    ~mesbox(struct_param($struct, processfailure));
    return;
}
if ($secondary_total < struct_param($struct, ingredient_secondary_count)) {
    ~mesbox(struct_param($struct, processfailure));
    return;
}

anim(human_furnace, 0);
sound_synth(furnace, 0, 0);

// display process message
mes(struct_param($struct, processmessage));

p_delay(3);

// delete main ore
inv_del(inv, struct_param($struct, ingredient), struct_param($struct, bar_count));
queue(smelting_repeat, 3, $last);
// delete secondary ore
if (struct_param($struct, ingredient_secondary) ! null) {
    inv_del(inv, struct_param($struct, ingredient_secondary), struct_param($struct, ingredient_secondary_count));
}
def_int $xp = struct_param($struct, productexp);
if ($bar = iron_bar) {
    if (inv_total(inv, e_bellows) > 0) {
        $xp = scale(5, 2, $xp); // 2.5x
        mes("Your smelting skills are showing!");
    } else {
        // if player is wearing ring of forging
        if (inv_total(worn, ring_of_forging) > 0) {
            ~lose_charge_ring_of_forging;
        } else if (randominc(1) = 1) { // else assume 50% chance of success
            mes("The ore is too impure and you fail to refine it.");
            return;
        }

    } 
}

// If player is wearing gold smith gauntlets, 2.5x xp. The multiplier was 1.5x in RSC,
// earliest mention of 2.5x (56.2) xp is in aug 2004: https://web.archive.org/web/20040820223401/http://runevillage.net/ThePub/viewtopic.php?p=1257194&amp
// No mentioned updates to Family Crest or the gauntlets in that period. 
// Was likely changed when ported to rs2.

if ($bar = gold_bar & inv_total(worn, gauntlets_of_goldsmithing) > 0) {
    $xp = scale(5, 2, $xp); // 2.5x
    mes("Your smelting skills are showing!");
}
if ($bar = steel_bar & inv_total(worn, steel_gauntlets) > 0) {
    $xp = scale(5, 2, $xp); // 2.5x
    mes("Your smelting skills are showing!");
}
stat_advance(smithing, $xp);

// add bar
if ($bar = bronze_bar) {
    %bronze_bar_smelted = calc(%bronze_bar_smelted + 1);
} else if ($bar = iron_bar) {
    %iron_bar_smelted = calc(%iron_bar_smelted + 1);
} else if ($bar = steel_bar) {
    %steel_bar_smelted = calc(%steel_bar_smelted + 1);
    %coal_smelted = calc(%coal_smelted + 2);
} else if ($bar = gold_bar) {
    %gold_bar_smelted = calc(%gold_bar_smelted + 1);
} else if ($bar = silver_bar) {
    %silver_bar_smelted = calc(%silver_bar_smelted + 1);
} else if ($bar = mithril_bar) {
    %mithril_bar_smelted = calc(%mithril_bar_smelted + 1);
    %coal_smelted = calc(%coal_smelted + 4);
} else if ($bar = adamantite_bar) {
    %adamant_bar_smelted = calc(%adamant_bar_smelted + 1);
    %coal_smelted = calc(%coal_smelted + 6);    
} else if ($bar = runite_bar) {
    %rune_bar_smelted = calc(%rune_bar_smelted + 1);
    %coal_smelted = calc(%coal_smelted + 8);
} else if ($bar = dragon_bar) {
    %dragon_bar_smelted = calc(%dragon_bar_smelted + 1);
    %coal_smelted = calc(%coal_smelted + 10);
    %rune_bar_smithed = calc(%rune_bar_smithed + 3);
}
    
inv_add(inv, $bar, 1);
// display product message
mes(struct_param($struct, productmessage));

//---queue to repeat smelting.
[queue,smelting_repeat](obj $last)
if(inv_total(inv, $last) > 0 & busy2 = false) {
    def_int $slot = ~inv_find(inv, $last, 0);
    if ($slot >= 0) {
        @smelt_ore(inv_getobj(inv, $slot));
    }
}